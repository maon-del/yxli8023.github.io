---
title: Mathematica高维度绘图及常用配色
tags: Study Mathematica Plot
layout: article
license: true
toc: true
pageview: true
aside:
    toc: true
sitemap: true
mathjax: true
author: YuXuan
show_author_profile: true
---
在使用origin作图的过程中，发现其配色不是很丰富，而且一些高维图和向量图的做法并不是特别的友好，所以通过一番探索终于在Mathematica上实现了我自己平时科研中需要用到的一些作图，最主要的高维度作图展示，以及一些等能面的绘制。由于Mathematica同时也可以用来进行计算，而且对数据的操作也是非常方便，所以干脆将所有形式的图都利用Mathematica来绘制，后面有精力准备写一个自己的包，日常调用绘图即可，同时自动设置号图像大小、坐标轴样式以及图例等等。
<!--more-->

# 体积图(三维空间中的密度分布)
**我所理解的体积图，就是在三维空间中的一个点(x,y,z)对应的密度大小是f，这也就是下面再构造数据的过程中，数据结构应该是(x,y,z,f)**
> 先产生一些需要用到的数据，至于具体为什么要这么做，这里就不解释了，可以自行通过后面的绘图命令所需要的参数去追究这个问题

```
vor1[x_, y_, z_] := 
 Module[{\[Zeta] = 1.0, \[CapitalDelta] = 0.5}, 
  If[y == 0 && z == 0 && x == 0, 
   0, \[CapitalDelta] Tanh[Sqrt[x^2 + y^2 + z^2]/\[Zeta]]*(x + I y)/
    Sqrt[x^2 + y^2 + z^2]]]
vor2[x_, y_] := 
 Module[{\[Zeta] = 1.0, \[CapitalDelta] = 0.5}, 
  If[y == 0 && x == 0, 
   0, \[CapitalDelta] Tanh[Sqrt[x^2 + y^2]/\[Zeta]]*(x + I y)/Sqrt[
    x^2 + y^2]]]
di = 0.2;
te = Abs@Table[
    vor1[x, y, z], {z, -10, 10, di}, {y, -10, 10, di}, {x, -10, 10, 
     di}];
te2 = Abs@
   Table[vor2[x, y], {z, -10, 10, di}, {y, -10, 10, di}, {x, -10, 10, 
     di}];
```

首先是3维空间中密度分布的一个切面图，只在一些确定的切面上展示，这个切面是可以自己指定的，我这里只是采用了默认选项

```
ListSliceDensityPlot3D[te2, "ZStackedPlanes", 
 ImageSize -> {Automatic, 800}, ColorFunction -> "IslandColors", 
 PlotLegends -> 
  Placed[BarLegend[Automatic, LegendMarkerSize -> {600, 40}, 
    LegendLayout -> "Row"], Above], 
 LabelStyle -> 
  Directive[Black, FontSize -> 24, FontFamily -> "Times New Roman"]]
```
![png](/assets/images/Mma/3d-1.png)

接下来就是3维空间中的密度分布了

```
ListDensityPlot3D[te, ColorFunction -> ColorData["RedBlueTones"], 
 PlotLegends -> 
  Placed[BarLegend[Automatic, LegendMarkerSize -> {20, 600}, 
    LabelStyle -> 
     Directive[Black, FontSize -> 24, 
      FontFamily -> "Times New Roman"]], {After, Top}], 
 ImageSize -> {Automatic, 800}, 
 LabelStyle -> 
  Directive[Black, FontSize -> 24, FontFamily -> "Times New Roman"]]
```

![png](/assets/images/Mma/3d-2.png)

```
ListDensityPlot3D[te2, ColorFunction -> ColorData["RedBlueTones"], 
 PlotLegends -> 
  Placed[BarLegend[Automatic, LegendMarkerSize -> {20, 600}, 
    LabelStyle -> 
     Directive[Black, FontSize -> 24, 
      FontFamily -> "Times New Roman"]], {After, Top}], 
 ImageSize -> {Automatic, 800}, 
 LabelStyle -> 
  Directive[Black, FontSize -> 24, FontFamily -> "Times New Roman"]]
```

![png](/assets/images/Mma/3d-3.png)

> **这里说明一下，图中的图例，字体，以及图例位置等一系列内容均是可以设置的，这些都可以在Mathematica的帮助文档里面找到，我这里也进行了一些基本的调整，大体上是满足我的需求的，如果你有别的需求，可以擦好看帮助文档进行修改，或者联系我，我们可以一起讨论解决**
上面这些都是利用Mathematica中算出来的数据做的图，平时可能需要结合其它程序计算数据，我这里顺便展示一下利用Fortran计算得到的数据如何做出上面的图形

```
phi = Import["phi0.dat"];
p2 = Map[Take[#, {1, 4}] &, phi];
ListDensityPlot3D[p2, ImageSize -> {Automatic, 700}, 
 ColorFunction -> ColorData["RedBlueTones"], 
 AxesStyle -> Directive[Orange, 24, FontFamily -> "Times New Roman"], 
 AxesLabel -> {"\!\(\*SubscriptBox[\(k\), \(x\)]\)/\[Pi]", 
   "\!\(\*SubscriptBox[\(k\), \(y\)]\)/\[Pi]", 
   "\!\(\*SubscriptBox[\(k\), \(z\)]\)/\[Pi]"}, 
 AxesStyle -> Directive[Orange, 24, FontFamily -> "Times New Roman"], 
 ViewPoint -> {1.3, -2.4, 2.}, 
 PlotLegends -> 
  Placed[BarLegend[Automatic, LegendMarkerSize -> {20, 530},
    LabelStyle -> 
     Directive[Black, FontSize -> 24, 
      FontFamily -> "Times New Roman"], Opacity[0.6]], {After, Top}]]
ListSliceDensityPlot3D[p2, "XStackedPlanes", 
 ImageSize -> {Automatic, 800}, ColorFunction -> "IslandColors", 
 PlotLegends -> 
  Placed[BarLegend[Automatic, LegendMarkerSize -> {600, 40}, 
    LegendLayout -> "Row"], Above], 
 LabelStyle -> 
  Directive[Black, FontSize -> 24, FontFamily -> "Times New Roman"], 
 AxesStyle -> Directive[Orange, 24, FontFamily -> "Times New Roman"], 
 AxesLabel -> {"\!\(\*SubscriptBox[\(k\), \(x\)]\)/\[Pi]", 
   "\!\(\*SubscriptBox[\(k\), \(y\)]\)/\[Pi]", 
   "\!\(\*SubscriptBox[\(k\), \(z\)]\)/\[Pi]"}, 
 AxesStyle -> Directive[Orange, 24, FontFamily -> "Times New Roman"]]
```

![png](/assets/images/Mma/3d-4.png)

这里需要注意的是，在导入数据时，数据文件的路径一定要正确，Mathematica默认的工作路径不是当前路径，而是你在安装软件时，会在windows系统的**文档**下产生一个文件夹，不过为了简便可以通过`SetDirectory[NotebookDirectory[]];`，将你Mathematica的笔记本当前所在路径设置为工作路径，这时候只要保证数据和笔记本文件在相同的路径下面即可。上图所用到的数据格式为(x,y,z,f)，即如下图所示

![png](/assets/images/Mma/3d-5.png)

如果你的数据格式和我上图所示一致，那么上面代码复制粘贴即可使用，记得修改文件名，若有疑问同样发email联系我，一起讨论解决。
# 二维密度图
## bubble密度图
首先示范利用气泡大小和颜色来标示2维平面上一点数值的大小，这个效果利用Mathematica始终没有做到我想要的效果，因为边界上留有很大的空隙，作图所需的数据在这里[TIldos](https://github.com/yxli8023/yxli8023.github.io/tree/master/assets/data)

```
x1 = Import["TIldos.dat"];
p1 = BubbleChart[x1, BubbleSizes -> {0.01, 0.1}, 
  BubbleScale -> "Diameter", ImageSize -> Large, 
  PlotRange -> {{0, 40},{0, 40}}, ColorFunction -> "Rainbow", 
  LabelStyle -> Directive[Large, Bold]]
```

![png](/assets/images/Mma/2d-1.png)

上图所需的数据结构为(x,y,z)，具体的可以参考上面体积图的数据结构，只不过是少了一列而已
## 颜色密度图

```
p1 = DensityPlot[Sin[x] Sin[y], {x, -4, 4}, {y, -3, 3}, 
  ColorFunction -> ColorData["RedBlueTones"], 
  ImageSize -> {800, Automatic}, 
  PlotLegends -> 
   Placed[BarLegend[Automatic, LegendMarkerSize -> {750, 40}, 
     LegendLayout -> "Row", 
     LabelStyle -> 
      Directive[Black, FontSize -> 24, 
       FontFamily -> "Times New Roman"]], Above], 
  FrameStyle -> Directive[Bold, 28, FontFamily -> "Times"], 
  FrameLabel -> {"\!\(\*SubscriptBox[\(k\), \(x\)]\)", 
    "\!\(\*SubscriptBox[\(k\), \(y\)]\)"}, 
  PlotRange -> {{-4, 4}, {-3, 3}}]
```

![png](/assets/images/Mma/2d-2.png)

```
p1 = DensityPlot[Sin[x] Sin[y], {x, -4, 4}, {y, -3, 3}, 
  ColorFunction -> ColorData["RedBlueTones"], 
  ImageSize -> {Automatic, 800}
  , PlotLegends -> 
   Placed[BarLegend[Automatic, LegendMarkerSize -> {40, 800}, 
     LabelStyle -> 
      Directive[Black, FontSize -> 24, 
       FontFamily -> "Times New Roman"]], {After, Top}], 
  FrameStyle -> Directive[Bold, 28, FontFamily -> "Times New Roman"], 
  FrameLabel -> {"\!\(\*SubscriptBox[\(k\), \(x\)]\)", 
    "\!\(\*SubscriptBox[\(k\), \(y\)]\)"}, 
  PlotRange -> {{-4, 4}, {-3, 3}}]
```

![png](/assets/images/Mma/2d-3.png)

Mathematica有很多种的配色方案，而且还可以通过选择周期表中的元素来决定颜色，可以参考[这里](https://reference.wolfram.com/language/ref/ColorData.html)，下面这张图展示了一些基本的配色，这些配色我在一些很好的文章中也经常看到，估计也是用Mathematica绘制的

![png](/assets/images/Mma/2d-4.png)

# 开边界能带图(多曲线绘图)
说成开边界能带图，如果熟悉凝聚态肯定知道是什么图，但其实说白了就是在一张图上画很多条曲线，这张图的数据在这里[KM-zigzag](https://github.com/yxli8023/yxli8023.github.io/tree/master/assets/data)

```
km = Transpose[Import["Km-zigzag.dat"]];
ListLinePlot[km[[2 ;;]], ImageSize -> Large, AspectRatio -> 1, 
 PlotTheme -> "Science", Frame -> True, 
 PlotStyle -> ColorData[17, "ColorList"], 
 FrameLabel -> {"\!\(\*SubscriptBox[\(k\), \(x\)]\)", "E"}, 
 FrameStyle -> Directive[Bold, 28, FontFamily -> "Times New Roman"], 
 PlotRange -> {{0, Length[Transpose@km] - 1}, {Min[km[[2 ;;]]], Max[km[[2 ;;]]]}}]
```

![png](/assets/images/Mma/2d-5.png)

> 在这个数据中，第一列代表的是kx的值，着这里先没有利用第一列的值，所以图中很坐标就只是数据的数量

```
km = Transpose[Import["Km-zigzag.dat"]];
Transpose[{km[[1]], km[[50]]}];
ListLinePlot[Map[Transpose[{km[[1]], #}] &, km[[2 ;;]]], 
 ImageSize -> Large, AspectRatio -> 1, PlotTheme -> "Science", 
 Frame -> True, PlotStyle -> ColorData[17, "ColorList"], 
 FrameLabel -> {"\!\(\*SubscriptBox[\(k\), \(x\)]\)/\[Pi]", "E"}, 
 FrameStyle -> Directive[Bold, 28, FontFamily -> "Times New Roman"], 
 PlotRange -> {{Min[km[[1]]], Max[km[[1]]]}, {Min[km[[2 ;;]]], 
    Max[km[[2 ;;]]]}}]
```

![png](/assets/images/Mma/2d-6.png)

> 利用第一列数据作为横坐标之后，作图范围既为0到$2pi$